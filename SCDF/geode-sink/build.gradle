buildscript {
    ext {
        springBootVersion = '2.1.6.RELEASE'
        confluentVersion = '4.1.0'
        spotbugsPluginVersion = '1.6.5'
        spotbugsVersion = '3.1.9'
        sherterGoogleJavaFormatPlugin = '0.8'
        googleJavaFormatVersion = '1.7'
    }
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:${spotbugsPluginVersion}")
        classpath("gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:${sherterGoogleJavaFormatPlugin}")

    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.spotbugs'
apply plugin: 'com.github.sherter.google-java-format'

group = 'io.enfuse.pipeline'
description = 'geode-sink'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    maven { url "http://packages.confluent.io/maven/" }
    mavenCentral()
}

ext {
    springCloudVersion = 'Greenwich.SR2'
}

spotbugs {
    toolVersion = spotbugsVersion
    ignoreFailures = false
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

googleJavaFormat {
    toolVersion = googleJavaFormatVersion
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.cloud:spring-cloud-stream')
    implementation('org.springframework.cloud:spring-cloud-stream-binder-kafka')
    implementation('org.springframework.data:spring-data-geode:2.1.9.RELEASE')
    implementation("org.springframework.boot:spring-boot-starter-log4j2")

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.cloud:spring-cloud-stream-test-support')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: "com.jayway.jsonpath", module:"json-path"
        exclude group: "org.skyscreamer", module:"jsonassert"
    }
}
