buildscript {
    ext {
        springBootVersion = '2.1.7.RELEASE'
        sherterGoogleJavaFormatPlugin = '0.8'
        googleJavaFormatVersion = '1.7'
    }
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:${sherterGoogleJavaFormatPlugin}")
        classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.22.1"

    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.sherter.google-java-format'
apply plugin: 'com.palantir.docker'

group = 'io.enfuse.pipeline'
description = 'geode-processor'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

bootJar {
    archiveName = 'gs-spring-boot-docker'
    version =  '0.1.0'
}

repositories {
    maven { url "http://packages.confluent.io/maven/" }
    maven { url 'https://repo.spring.io/milestone' }
    mavenCentral()
}

ext {
    springCloudVersion = 'Greenwich.SR2'
}

googleJavaFormat {
    toolVersion = googleJavaFormatVersion
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.cloud:spring-cloud-stream')
    implementation('org.springframework.cloud:spring-cloud-stream-binder-kafka')
    implementation('org.json:json:20180813')
    implementation('io.micrometer:micrometer-registry-prometheus:latest.release')

    implementation('org.springframework.geode:spring-geode-starter:1.1.0.M3')
    implementation('org.springframework.geode:spring-geode-starter-actuator:1.1.0.RC1')
    implementation('org.springframework.boot:spring-boot-configuration-processor')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.cloud:spring-cloud-stream-test-support')
}

test.dependsOn(
    tasks.googleJavaFormat,
    tasks.verifyGoogleJavaFormat
)


dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: "com.jayway.jsonpath", module:"json-path"
        exclude group: "org.skyscreamer", module:"jsonassert"
    }
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/libs")
}
docker {
    name "${project.group}/${bootJar.baseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}